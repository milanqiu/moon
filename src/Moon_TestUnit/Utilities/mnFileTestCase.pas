unit mnFileTestCase;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit
  being tested.

}

interface

uses
  TestFramework;

type
  TmnFileTestCase = class(TTestCase)
  strict private
  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
    procedure testExtractFileNoExt;
    procedure testExtractPureFileName;
    procedure testExtractParentPath;
    procedure testCorrectPath;
    procedure testCorrectDir;
    procedure testAppendFileNameSuffix;
    procedure testGetAbsoluteFileName;
    procedure testGetRelativeFileName;
    procedure testGetFileList;
    procedure testSearchFile;
    procedure testDeleteDir;
    procedure testCopyAndDeleteFile;
    procedure testGetFileSize;
    procedure testCanFileBeRW;
    procedure testSaveLoadStrFile;
    procedure testAppendStrToFile;
    procedure testIsSameFile;
    procedure testIsSameExcelFile;
    procedure testIsSameWordFile;
    procedure testGetTextFileEncoding;
    procedure testValidateFileExists;
  end;

implementation

uses mnFile, mnSystem, mnResStrsU, mnDebug, SysUtils, UTestConsts, mnString, mnCOM, StrUtils, Classes,
  Forms, ComCtrls, Windows;

{ TmnFileTestCase }

procedure TmnFileTestCase.SetUp;
begin
end;

procedure TmnFileTestCase.TearDown;
begin
end;

procedure TmnFileTestCase.testExtractFileNoExt;
begin
  CheckEquals(mnExtractFileNoExt('C:\aaa\bbb\ccc.txt'), 'C:\aaa\bbb\ccc');
  CheckEquals(mnExtractFileNoExt('C:\aaa\bbb\'), 'C:\aaa\bbb\');
  CheckEquals(mnExtractFileNoExt('C:\aaa.txt'), 'C:\aaa');
  CheckEquals(mnExtractFileNoExt('C:\aaa'), 'C:\aaa');
  CheckEquals(mnExtractFileNoExt('aaa.txt'), 'aaa');
end;

procedure TmnFileTestCase.testExtractPureFileName;
begin
  CheckEquals(mnExtractPureFileName('C:\aaa\bbb\ccc.txt'), 'ccc');
  CheckEquals(mnExtractPureFileName('C:\aaa\bbb\'), '');
  CheckEquals(mnExtractPureFileName('C:\aaa.txt'), 'aaa');
  CheckEquals(mnExtractPureFileName('C:\aaa'), 'aaa');
  CheckEquals(mnExtractPureFileName('aaa.txt'), 'aaa');
end;

procedure TmnFileTestCase.testExtractParentPath;
begin
  CheckEquals(mnExtractParentPath('C:\aaa\bbb\ccc\ddd.txt'), 'C:\aaa\bbb\');
  CheckEquals(mnExtractParentPath('C:\aaa\bbb\ccc\'), 'C:\aaa\bbb\');
  CheckEquals(mnExtractParentPath('C:\aaa\bbb\ccc'), 'C:\aaa\');
  CheckEquals(mnExtractParentPath('C:\aaa'), 'C:\');
  CheckEquals(mnExtractParentPath('C:\'), 'C:\');
  CheckEquals(mnExtractParentPath('C:'), 'C:\');
end;

procedure TmnFileTestCase.testCorrectPath;
begin
  CheckEquals(mnCorrectPath('C:\aaa'), 'C:\aaa\');
  CheckEquals(mnCorrectPath('C:\aaa\'), 'C:\aaa\');
end;

procedure TmnFileTestCase.testCorrectDir;
begin
  CheckEquals(mnCorrectDir('C:\aaa'), 'C:\aaa');
  CheckEquals(mnCorrectDir('C:\aaa\'), 'C:\aaa');
end;

procedure TmnFileTestCase.testAppendFileNameSuffix;
begin
  CheckEquals(mnAppendFileNameSuffix('C:\aaa\bbb\ccc.txt', '_adv'), 'C:\aaa\bbb\ccc_adv.txt');
  CheckEquals(mnAppendFileNameSuffix('C:\aaa\bbb\',        '_adv'), 'C:\aaa\bbb\_adv');
  CheckEquals(mnAppendFileNameSuffix('C:\aaa.txt',         '_adv'), 'C:\aaa_adv.txt');
  CheckEquals(mnAppendFileNameSuffix('C:\aaa',             '_adv'), 'C:\aaa_adv');
  CheckEquals(mnAppendFileNameSuffix('aaa.txt',            '_adv'), 'aaa_adv.txt');
end;

procedure TmnFileTestCase.testGetAbsoluteFileName;
begin
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', 'ccc.txt'), 'C:\aaa\bbb\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '.\ccc.txt'), 'C:\aaa\bbb\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', 'eee\ccc.txt'), 'C:\aaa\bbb\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '\eee\ccc.txt'), 'C:\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '.\eee\ccc.txt'), 'C:\aaa\bbb\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '..\ccc.txt'), 'C:\aaa\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '..\..\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '..\..\..\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', 'C:\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', 'D:\ccc.txt'), 'D:\ccc.txt');

  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', 'ooo\'), 'C:\aaa\bbb\ooo\');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb\', '..\ooo\'), 'C:\aaa\ooo\');

  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', 'ccc.txt'), 'C:\aaa\bbb\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '.\ccc.txt'), 'C:\aaa\bbb\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', 'eee\ccc.txt'), 'C:\aaa\bbb\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '\eee\ccc.txt'), 'C:\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '.\eee\ccc.txt'), 'C:\aaa\bbb\eee\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '..\ccc.txt'), 'C:\aaa\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '..\..\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '..\..\..\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', 'C:\ccc.txt'), 'C:\ccc.txt');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', 'D:\ccc.txt'), 'D:\ccc.txt');

  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', 'ooo\'), 'C:\aaa\bbb\ooo\');
  CheckEquals(mnGetAbsoluteFileName('C:\aaa\bbb', '..\ooo\'), 'C:\aaa\ooo\');
end;

procedure TmnFileTestCase.testGetRelativeFileName;
begin
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\bbb\ccc.txt'), '.\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\bbb\eee\ccc.txt'), '.\eee\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\ccc.txt'), '..\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\ccc.txt'), '..\..\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'D:\ccc.txt'), '');

  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\bbb\ooo\'), '.\ooo\');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\eee\ooo\'), '..\eee\ooo\');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb\', 'C:\aaa\ooo\'), '..\ooo\');

  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\bbb\ccc.txt'), '.\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\bbb\eee\ccc.txt'), '.\eee\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\ccc.txt'), '..\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\ccc.txt'), '..\..\ccc.txt');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'D:\ccc.txt'), '');

  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\bbb\ooo\'), '.\ooo\');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\eee\ooo\'), '..\eee\ooo\');
  CheckEquals(mnGetRelativeFileName('C:\aaa\bbb', 'C:\aaa\ooo\'), '..\ooo\');
end;

procedure TmnFileTestCase.testGetFileList;
var
  Dir: string;
  File1, File2, File3, SubDir, SubDirFile: string;
  FileList: mnTStrList;
begin
  Dir := mnTProjectConvention.GetTestTempPathSub('testGetFileList');
  File1 := Dir + '\File1';
  File2 := Dir + '\File2.txt';
  File3 := Dir + '\File3.html';
  SubDir := Dir + '\SubDir';
  SubDirFile := SubDir + '\SubDirFile.txt';
  ForceDirectories(Dir);
  ForceDirectories(SubDir);
  mnSaveStrToFile('File1', File1);
  mnSaveStrToFile('File2', File2);
  mnSaveStrToFile('File3', File3);
  mnSaveStrToFile('SubDirFile', SubDirFile);

  FileList := mnTStrList.Create;
  try
    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList, []);
    CheckEquals(FileList.Count, 4);
    CheckEquals(FileList[0], 'File1');
    CheckEquals(FileList[1], 'File2');
    CheckEquals(FileList[2], 'File3');
    CheckEquals(FileList[3], 'SubDir');

    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList, [floIncludePath]);
    CheckEquals(FileList.Count, 4);
    CheckEquals(FileList[0], Dir+'\File1');
    CheckEquals(FileList[1], Dir+'\File2');
    CheckEquals(FileList[2], Dir+'\File3');
    CheckEquals(FileList[3], Dir+'\SubDir');

    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList, [floIncludeExt]);
    CheckEquals(FileList.Count, 4);
    CheckEquals(FileList[0], 'File1');
    CheckEquals(FileList[1], 'File2.txt');
    CheckEquals(FileList[2], 'File3.html');
    CheckEquals(FileList[3], 'SubDir');

    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList);
    CheckEquals(FileList.Count, 4);
    CheckEquals(FileList[0], Dir+'\File1');
    CheckEquals(FileList[1], Dir+'\File2.txt');
    CheckEquals(FileList[2], Dir+'\File3.html');
    CheckEquals(FileList[3], Dir+'\SubDir');

    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList, [floDirectoryOnly]);
    CheckEquals(FileList.Count, 1);
    CheckEquals(FileList[0], 'SubDir');

    FileList.Clear;
    mnGetFileList(Dir+'\*', FileList, [floFileOnly]);
    CheckEquals(FileList.Count, 3);
    CheckEquals(FileList[0], 'File1');
    CheckEquals(FileList[1], 'File2');
    CheckEquals(FileList[2], 'File3');
  finally
    FileList.Free;
    mnDeleteDir(Dir);
  end;
end;

procedure TmnFileTestCase.testSearchFile;
var
  Dir: string;
  File1, File2, File3, SubDir, SubDirFile: string;
  FileList: mnTStrList;
begin
  Dir := mnTProjectConvention.GetTestTempPathSub('testGetFileList');
  File1 := Dir + '\File1';
  File2 := Dir + '\File2.txt';
  File3 := Dir + '\File3.html';
  SubDir := Dir + '\SubDir';
  SubDirFile := SubDir + '\SubDirFile.txt';
  ForceDirectories(Dir);
  ForceDirectories(SubDir);
  mnSaveStrToFile('File1', File1);
  mnSaveStrToFile('File2', File2);
  mnSaveStrToFile('File3', File3);
  mnSaveStrToFile('SubDirFile', SubDirFile);

  FileList := mnTStrList.Create;
  try
    FileList.Clear;
    mnSearchFile(Dir, '*', FileList, []);
    CheckEquals(FileList.Count, 6);
    CheckEquals(FileList[0], 'testGetFileList');
    CheckEquals(FileList[1], 'File1');
    CheckEquals(FileList[2], 'File2');
    CheckEquals(FileList[3], 'File3');
    CheckEquals(FileList[4], 'SubDir');
    CheckEquals(FileList[5], 'SubDirFile');

    FileList.Clear;
    mnSearchFile(Dir, '*.txt', FileList, [floIncludePath]);
    CheckEquals(FileList.Count, 2);
    CheckEquals(FileList[0], Dir+'\File2');
    CheckEquals(FileList[1], Dir+'\SubDir\SubDirFile');

    FileList.Clear;
    mnSearchFile(Dir, '*.txt', FileList, [floIncludeExt]);
    CheckEquals(FileList.Count, 2);
    CheckEquals(FileList[0], 'File2.txt');
    CheckEquals(FileList[1], 'SubDirFile.txt');

    FileList.Clear;
    mnSearchFile(Dir, 'Fi*', FileList);
    CheckEquals(FileList.Count, 3);
    CheckEquals(FileList[0], Dir+'\File1');
    CheckEquals(FileList[1], Dir+'\File2.txt');
    CheckEquals(FileList[2], Dir+'\File3.html');

    FileList.Clear;
    mnSearchFile(Dir, '*', FileList, [floDirectoryOnly]);
    CheckEquals(FileList.Count, 2);
    CheckEquals(FileList[0], 'testGetFileList');
    CheckEquals(FileList[1], 'SubDir');

    FileList.Clear;
    mnSearchFile(Dir, '*', FileList, [floFileOnly]);
    CheckEquals(FileList.Count, 4);
    CheckEquals(FileList[0], 'File1');
    CheckEquals(FileList[1], 'File2');
    CheckEquals(FileList[2], 'File3');
    CheckEquals(FileList[3], 'SubDirFile');
  finally
    FileList.Free;
    mnDeleteDir(Dir);
  end;
end;

procedure TmnFileTestCase.testDeleteDir;
var
  Dir: string;
begin
  Dir := mnTProjectConvention.GetTestTempPathSub('testDeleteDir');

  ForceDirectories(Dir + '\Useless');
  mnSaveStrToFile('Useless1', Dir + '\Useless1.txt');
  mnSaveStrToFile('Useless2', Dir + '\Useless\Useless2.txt');

  Check(DirectoryExists(Dir));
  Check(mnDeleteDir(Dir));
  CheckFalse(DirectoryExists(Dir));
end;

procedure TmnFileTestCase.testCopyAndDeleteFile;
var
  SrcFile, DstFile: string;
begin
  SrcFile := mnAppPathSub('dunit.ini');
  DstFile := mnTProjectConvention.GetTestTempPathSub('dunit_copy.ini');

  Check(FileExists(SrcFile));
  CheckFalse(FileExists(DstFile));

  Check(mnCopyFile(SrcFile, DstFile));
  Check(FileExists(DstFile));

  CheckFalse(mnCopyFile(SrcFile, DstFile, True));
  Check(mnCopyFile(SrcFile, DstFile, False));

  Check(mnDeleteFile(DstFile));
  CheckFalse(FileExists(DstFile));

  CheckFalse(mnDeleteFile(DstFile));
end;

procedure TmnFileTestCase.testGetFileSize;
var
  i: Integer;
  str: string;
  TempFile: string;
begin
  str := '';
  for i := 0 to 255 do
    str := str + Chr(i);
  str := str + '一二三四五';

  TempFile := mnTProjectConvention.GetTestTempPathSub('temp.bin');
  mnSaveStrToFile(str, TempFile);
  CheckEquals(mnGetFileSize(TempFile), 256+10);
  Check(mnDeleteFile(TempFile));

  CheckEquals(mnGetFileSize(mnTProjectConvention.GetFilesPathSub('Images\BMP.bmp')), 14+40+20*20*3);
end;

procedure TmnFileTestCase.testCanFileBeRW;
var
  i: Integer;
  str: string;
  TempFile: string;
  HFileHandle: HFILE;
begin
  str := '';
  for i := 0 to 255 do
    str := str + Chr(i);
  str := str + '一二三四五';

  TempFile := mnTProjectConvention.GetTestTempPathSub('temp.bin');
  mnSaveStrToFile(str, TempFile);
  Check(mnCanFileBeRW(TempFile));

  HFileHandle := CreateFile(PChar(TempFile), GENERIC_READ or GENERIC_WRITE, 0, nil, OPEN_EXISTING, FILE_ATTRIBUTE_NORMAL, 0);
  CheckFalse(mnCanFileBeRW(TempFile));

  CloseHandle(HFileHandle);
  Check(mnCanFileBeRW(TempFile));

  Check(mnDeleteFile(TempFile));
  CheckFalse(mnCanFileBeRW(TempFile));
end;

procedure TmnFileTestCase.testSaveLoadStrFile;
var
  i: Integer;
  str: string;
  TempFile: string;
begin
  str := '';
  for i := 0 to 255 do
    str := str + Chr(i);
  str := str + '一二三四五';

  TempFile := mnTProjectConvention.GetTestTempPathSub('temp.bin');
  mnSaveStrToFile(str, TempFile);
  Check(str = mnLoadStrFromFile(TempFile));
  Check(mnDeleteFile(TempFile));

  try
    mnLoadStrFromFile(mnTProjectConvention.GetTestTempPathSub('fake.bin'));
    mnNeverGoesHere;
  except
    on E: Exception do CheckEquals(E.Message, Format(SFileNotExists, [mnTProjectConvention.GetTestTempPathSub('fake.bin')]));
  end;
end;

procedure TmnFileTestCase.testAppendStrToFile;
var
  i: Integer;
  str: string;
  TempFile: string;
begin
  str := '';
  for i := 0 to 255 do
    str := str + Chr(i);
  str := str + '一二三四五';

  TempFile := mnTProjectConvention.GetTestTempPathSub('temp.bin');
  mnSaveStrToFile(str, TempFile);
  mnAppendStrToFile(str, TempFile);
  mnAppendStrToFile(str, TempFile);
  Check(DupeString(str, 3) = mnLoadStrFromFile(TempFile));
  Check(mnDeleteFile(TempFile));
end;

procedure TmnFileTestCase.testIsSameFile;
var
  i: Integer;
  str1, str2, str3: string;
  TempFile1, TempFile2, TempFile3: string;
begin
  str1 := '';
  for i := 0 to 255 do
    str1 := str1 + Chr(i);
  str1 := str1 + '一二三四五';
  str2 := str1;
  str2[2] := Chr(1);
  str3 := str1;
  str3[2] := 'a';

  TempFile1 := mnTProjectConvention.GetTestTempPathSub('temp1.bin');
  TempFile2 := mnTProjectConvention.GetTestTempPathSub('temp2.bin');
  TempFile3 := mnTProjectConvention.GetTestTempPathSub('temp3.bin');

  mnSaveStrToFile(str1, TempFile1);
  mnSaveStrToFile(str2, TempFile2);
  mnSaveStrToFile(str3, TempFile3);

  Check(mnIsSameFile(TempFile1, TempFile2));
  CheckFalse(mnIsSameFile(TempFile1, TempFile3));

  Check(mnDeleteFile(TempFile1));
  Check(mnDeleteFile(TempFile2));
  Check(mnDeleteFile(TempFile3));
end;

procedure TmnFileTestCase.testIsSameExcelFile;
var
  ExcelA, ExcelB: mnTExcel;
  ExcelFileA, ExcelFileB: string;
begin
  ExcelFileA := mnTProjectConvention.GetTestTempPathSub('ExcelA.xlsx');
  ExcelFileB := mnTProjectConvention.GetTestTempPathSub('ExcelB.xlsx');
  ExcelA := mnTExcel.Create;
  ExcelB := mnTExcel.Create;
  try
    ExcelA.New;
    ExcelB.New;

    ExcelA.Cells[2, 3].Value := Str_0;
    ExcelA.Cells[2, 4].Value := Int_0;
    ExcelA.Cells[2, 5].Value := Float_0;
    ExcelA.Cells[3, 3].Value := DT_0;
    ExcelA.Cells[3, 4].Value := Curr_0;
    ExcelA.Cells[3, 5].Value := Bool_0;

    ExcelB.Cells[2, 3].Value := Str_0;
    ExcelB.Cells[2, 4].Value := Int_0;
    ExcelB.Cells[2, 5].Value := Float_0;
    ExcelB.Cells[3, 3].Value := DT_0;
    ExcelB.Cells[3, 4].Value := Curr_0;
    ExcelB.Cells[3, 5].Value := Bool_0;

    ExcelA.SaveAs(ExcelFileA);
    ExcelB.SaveAs(ExcelFileB);
  finally
    ExcelA.Free;
    ExcelB.Free;
  end;

  Check(mnIsSameExcelFile(ExcelFileA, ExcelFileB));
  Check(mnDeleteFile(ExcelFileA));
  Check(mnDeleteFile(ExcelFileB));
end;

procedure TmnFileTestCase.testIsSameWordFile;
var
  WordA, WordB: mnTWord;
  WordFileA, WordFileB: string;
begin
  WordFileA := mnTProjectConvention.GetTestTempPathSub('WordA.docx');
  WordFileB := mnTProjectConvention.GetTestTempPathSub('WordB.docx');
  WordA := mnTWord.Create;
  WordB := mnTWord.Create;
  try
    WordA.New;
    WordB.New;

    WordA.Global.Text := Str_0;
    WordA.Header.Text := Str_1;
    WordA.Footer.Text := Str_2;

    WordB.Global.Text := Str_0;
    WordB.Header.Text := Str_1;
    WordB.Footer.Text := Str_2;

    WordA.SaveAs(WordFileA);
    WordB.SaveAs(WordFileB);
  finally
    WordA.Free;
    WordB.Free;
  end;

  Check(mnIsSameWordFile(WordFileA, WordFileB));
  Check(mnDeleteFile(WordFileA));
  Check(mnDeleteFile(WordFileB));
end;

procedure TmnFileTestCase.testGetTextFileEncoding;
var
  TempFile: string;
begin
  TempFile := mnTProjectConvention.GetTestTempPathSub('temp.txt');

  mnSaveStrToFile(Text_Ansi, TempFile);
  Check(mnGetTextFileEncoding(TempFile) = teAnsi);

  mnSaveStrToFile(Text_UnicodeLE_WithBOM, TempFile);
  Check(mnGetTextFileEncoding(TempFile) = teUnicodeLE);

  mnSaveStrToFile(Text_UnicodeBE_WithBOM, TempFile);
  Check(mnGetTextFileEncoding(TempFile) = teUnicodeBE);

  mnSaveStrToFile(Text_UTF8_WithBOM, TempFile);
  Check(mnGetTextFileEncoding(TempFile) = teUTF8);
  
  Check(mnDeleteFile(TempFile));
end;

procedure TmnFileTestCase.testValidateFileExists;
begin
  mnValidateFileExists(mnAppPathSub('dunit.ini'));

  try
    mnValidateFileExists(mnTProjectConvention.GetTestTempPathSub('fake'));
    mnNeverGoesHere;
  except
    on E: Exception do CheckEquals(E.Message, Format(SFileNotExists, [mnTProjectConvention.GetTestTempPathSub('fake')]));
  end;

  try
    mnValidateFileExists(mnTProjectConvention.GetTestTempPathSub('fake'), '%s 文件不存在');
    mnNeverGoesHere;
  except
    on E: Exception do CheckEquals(E.Message, Format('%s 文件不存在', [mnTProjectConvention.GetTestTempPathSub('fake')]));
  end;
end;

initialization
  // Register any test cases with the test runner
  RegisterTest(TmnFileTestCase.Suite);
end.


